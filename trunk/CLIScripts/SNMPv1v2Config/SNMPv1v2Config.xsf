enable cli scripting

# @METADATASTART
#@DetailDescriptionStart
#############################################################################
#
# Script        : SNMPv1v2Config
# Revision      : 1.0
# Last Updated  : Feburary 29, 2008
#
# Purpose:  This script provides an example of SNMP V1/V2 configuration for
#           an ExtremeXOS(TM) switch.  In this example:
#
#           1.  SNMP v1/v2 access is configured
#           2.  SNMP switch name, location, and contact is created
#           3.  Optionally, trap receivers are created 
#           4.  Optionally tailored read/write community strings are created
#           5.  Optionally, standard read/write community strings are removed
#     
#           Inversely, this script can be used to disable SNMP v1/V2 access.
#
#############################################################################
#@DetailDescriptionEnd


############################################################################# 
# SNMP v1/v2 Variable definitions
#############################################################################

# @VariableFieldLabel "If this script encounters errors, do you wish to abort or ignore?"
set var clierrormode abort

# @VariableFieldLabel Congifigure SNMP v1/v2 access? (yes or no)"
set var ynsnmpconfig yes

# @VariableFieldLabel "Disable SNMP v1/v2 access? (yes or no)"
set var ynsnmpdisable no

# @VariableFieldLabel "Add SNMP v1/v2 communities? (yes or no)"
set var ynsnmpcommadd yes

# @VariableFieldLabel "Read/Write SNMP Community Name?"
set var snmprwname 234x44#84h

# @VariableFieldLabel "Read-Only SNMP Community Name?"
set var snmproname readonly34684

# @VariableFieldLabel "Remove default SNMP Communities? (yes or no)"
set var ynsnmpcommrem yes

# @VariableFieldLabel "SNMP Switch Name?"
set var snmpname IDF-A

# @VariableFieldLabel "SNMP Location?"
set var snmplocation Everywhere_USA

# @VariableFieldLabel "SNMP Contact?"
set var snmpcontact NOC_John_Doe

# @VariableFieldLabel "Number of SNMP Trap Receivers (Script supports: 1-3)"
set var snmptrapcount 2

# @VariableFieldLabel "SNMP Trap Receiver #1"
set var snmptrap1 10.10.10.10

# @VariableFieldLabel "SNMP Trap Receiver #2"
set var snmptrap2 10.10.11.10

# @VariableFieldLabel "SNMP Trap Receiver #3"
set var snmptrap3 10.10.12.10

# @MetaDataEnd

#############################################################################
# SNMP V1/V2 Configuration
#############################################################################

if (!$match($clierrormode,ignore)) then
  configure cli mode scripting ignore-error
  create log entry "CLI mode set for Ignore on Error"
else
  configure cli mode scripting abort-on-error
  create log entry "CLI mode set for Abort on Error"
endif

if (!$match($ynsnmpconfig,yes)) then
  create log entry "Starting SNMP Configuration"

  configure snmp sysName "$(snmpname)"
  configure snmp sysLocation "$(snmplocation)"
  configure snmp sysContact "$(snmpcontact)"

  if ($snmptrapcount >= 1) then 
    configure snmp add trapreceiver $snmptrap1 community $snmproname
  endif
  if ($snmptrapcount >= 2) then 
    configure snmp add trapreceiver $snmptrap2 community $snmproname
  endif
  if ($snmptrapcount >= 3) then 
    configure snmp add trapreceiver $snmptrap3 community $snmproname
  endif

  if (!$match($ynsnmpcommadd,yes)) then
    configure snmp add community readwrite $snmprwname
    configure snmp add community readonly $snmproname
    create log entry "New SNMP Communities Created"
  endif

  if (!$match($ynsnmpcommrem,yes)) then 
    configure snmp delete community readwrite private
    configure snmp delete community readonly public
    create log entry "Default SNMP Communities Removed"
  else
    create log entry "Default SNMP Communities NOT Removed"
  endif

else

  if (!$match($ynsnmpdisable,yes)) then
    create log entry "Disabling SNMP access"
    disable snmp access snmp-v1v2
  else
    create log entry "SNMP Not Configured"
  endif

endif

disable cli scripting