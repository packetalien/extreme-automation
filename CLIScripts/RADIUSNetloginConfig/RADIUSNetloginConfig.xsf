enable cli scripting

# @METADATASTART
#@DetailDescriptionStart
#############################################################################
#
# Script        : RADIUSNetloginConfig
# Revision      : 1.1
# Last Updated  : April 4, 2008
#
# Purpose:  This script provides an example of configuring an ExtremeXOS(TM)
#           switch with RADIUS server netlogin information.  Specifically,
#
#           1.  Configure RADIUS netlogin information
#           2.  Configure VLAN for RADIUS server
#           3.  Enable RADIUS netlogin
#
# Note:  The RADIUS Client IP address must be configured in specified 
#        virtual router name (vr-default) for this script to execute 
#        properly.
#
#############################################################################
#@DetailDescriptionEnd

#############################################################################
# RADIUS and Netlogin Variable definitions
#############################################################################

# @VariableFieldLabel "If this script encounters errors, do you wish to abort or ignore?"
set var clierrormode abort

# @VariableFieldLabel "Configure and enable RADIUS for Network Login? (yes or no)"
set var ynradiusnetlogin yes

# @VariableFieldLabel "Is The RADIUS Server Shared Secret Encrypted (yes or no)"
set var ynradsecretnetlogin no

# @VariableFieldLabel "RADIUS Server IP Address"
set var radservernetlogin 10.10.10.144

# @VariableFieldLabel "RADIUS Client IP Address"
# Note:  RADIUS Client IP Address must be configured in virtual router
#        "VR-Default" for this script to execute properly
set var radclientnetlogin 10.10.10.1

# @VariableFieldLabel "RADIUS Client IP Address Virtual Router Name (vr-default)"
set var vrname vr-default

# @VariableFieldLabel "RADIUS Server Shared Secret Password"
set var radsecretnetlogin goextreme

# @VariableFieldLabel "Network Login Authentication Order?"
set var netloginorder "dot1x mac"

# @VariableFieldLabel "Network Login Port List"
set var netloginports 1-24

# @VariableFieldLabel "Network Login GUEST VLAN"
set var guestvlan guestvlan

# @MetaDataEnd

#############################################################################
# RADIUS NetLogin
#############################################################################

create log entry "Configuring RADIUS Netlogin"

if (!$match($clierrormode,ignore)) then
  configure cli mode scripting ignore-error
  create log entry "CLI mode set for Ignore on Error"
else
  configure cli mode scripting abort-on-error
  create log entry "CLI mode set for Abort on Error"
endif

if (!$match($ynradsecretnetlogin,yes)) then
  configure radius netlogin primary shared-secret encrypted $radsecretnetlogin
  create log entry "Config RADIUS secret encrypted"
else
  configure radius netlogin primary shared-secret $radsecretnetlogin
  create log entry "Config RADIUS secret NOT encrypted"
endif

if (!$match($ynradiusnetlogin,yes)) then
  create log entry "Starting Network Login Configuration"
  create vlan $guestvlan
  configure netlogin vlan $guestvlan
  configure default delete port $netloginports
  enable netlogin dot1x
  enable netlogin mac
  enable netlogin ports $netloginports $netloginorder
  configure netlogin ports $netloginports mode mac-based-vlans
  configure radius netlogin primary server $radservernetlogin client-ip $radclientnetlogin vr $vrname
  enable radius netlogin
  create log entry "RADIUS Network Login Configuration Complete"
else
  create log entry "No RADIUS Netlogin"
endif

disable cli scripting

