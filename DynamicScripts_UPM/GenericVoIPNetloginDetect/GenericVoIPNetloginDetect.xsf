enable cli scripting

# @METADATASTART
#@DetailDescriptionStart
#
# Script        : GenericVoIPNetloginDetect
# Revision      : 1.3
# Last Updated  : August 5, 2008
#
# Purpose:  This profile is an example on how one would configure network
#           parameters for 802.1x or MAC authenticated VoIP phones that do not
#           use LLDP for provisioning.  It is assumed that specific phone
#           provisioning is accomplished through Dynamic Host Configuration
#           Protocol (DHCP) server options.
#
#			A ReadMe file for this widget is available at:
#			http://www.extremenetworks.com/solutions/widget-central/
#
#@DetailDescriptionEnd

# Variable definitions

# @VariableFieldLabel "If this script encounters errors, do you wish to abort or ignore?"
set var clierrormode abort

# @VariableFieldLabel "Assign Port to QoS Profile (true or false)"
set var assignqos true

# @VariableFieldLabel "SummitStack(TM) Switch (yes or no)"
set var sstack no

# @MetaDataEnd
# Generic VoIP Device Authenticated Provisioning

create log entry "Starting Generic VoIP Device Authenticated Provisioning"

if (!$match($clierrormode,ignore)) then
  configure cli mode scripting ignore-error
  create log entry "CLI mode set for Ignore on Error"
else
  configure cli mode scripting abort-on-error
  create log entry "CLI mode set for Abort on Error"
endif

# Determine if "Device Authenticated" on Port

if (!$match($EVENT.NAME,USER-AUTHENTICATED)) then
  create log entry "Device Authenticated on Port"
# Determine QoS Settings
  if (!$match($assignqos,true)) then
    if (!$match($sstack,no)) then
      create log entry "Assigning port to QoS profile QP7"
      configure ports $EVENT.USER_PORT qosprofile QP7
    else
      create log entry "Assigning port to QoS profile QP5" 
      configure ports $EVENT.USER_PORT qosprofile QP5
    endif
  endif

# Configure Port Label
  configure port $EVENT.USER_PORT display-string $EVENT.USER_PORT-VOIP

  
# Determine if "Device Undetect" on Port

else
  
  if (!$match($EVENT.NAME,USER-UNAUTHENTICATED)) then
    create log entry "Device Unauthenticated on Port"
# Reset QoS Setting on Port
    if (!$match($assignqos,true)) then
      create log entry "Unconfiguring QoS"
      configure ports $EVENT.USER_PORT qosprofile none
    endif
    
# Clear Port Label   
    unconfigure port $EVENT.USER_PORT display-string
    
    
  endif

endif

create log entry "End Generic VoIP Device Authenticated Provisioning"

disable cli scripting